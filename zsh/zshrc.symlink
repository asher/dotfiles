if [[ "$PROFILE_STARTUP" == true ]]; then
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2> /tmp/startlog.$$
    setopt xtrace prompt_subst
fi

ZSH=$HOME/.oh-my-zsh
DOTFILES=$HOME/.dotfiles

# oh-mh-zsh theme
ZSH_THEME="powerlevel9k/powerlevel9k"
POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(status root_indicator background_jobs)

# oh-my-zsh aliases
alias zshconfig="st ~/.zshrc"
alias ohmyzsh="st ~/.oh-my-zsh"

# disable update, upgrade_dotfiles handles this
DISABLE_AUTO_UPDATE="true"

upgrade_dotfiles() {
  if [[ `pwd` == $HOME/.dotfiles ]] ; then
    ./bootstrap.sh update
  else
    pushd ~/.dotfiles > /dev/null
    ./bootstrap.sh update
    popd > /dev/null
  fi
}

# for the over eager upgrade junky
yippee-ki-yay() {
    upgrade_dotfiles
}

plugins=("${(@f)$(
# define oh-my-zsh plugins implicitly based on topics
find $DOTFILES $DOTFILES/local/ -not -name '.git' -d 1 -type d -exec basename {} \;

# add any plugins defined by files
find -L $DOTFILES -name oh-my-zsh.plugins -d 2 -exec cat {} \;
)}")

# load oh-my-zsh
source $ZSH/oh-my-zsh.sh

# use .localrc for things that need to be kept secret
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# make sure DEFAULT_USER has been set
if [ -z "$DEFAULT_USER" ]; then
  echo "Please set DEFAULT_USER in your ~/.localrc"
  exit 1
fi

# all of our zsh files
typeset -U config_files
config_files=($(find -L "$DOTFILES" -name \*.zsh))

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# put the bin/ directories first on the path
export PATH=$DOTFILES/bin:$DOTFILES/local/bin:$PATH

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
